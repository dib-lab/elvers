from os.path import join

assembly_dir= config['eelpond_directories']['outdirs']['assemble']
quant_dir= config['eelpond_directories']['outdirs']['quantify']

deseq_params = config['deseq2']['program_params']
ep_params = config['deseq2']['eelpond_params']
deseq_dir = config['deseq2']['eelpond_params']['outdir']

def get_deseq2_threads(wildcards=None):
    # https://github.com/snakemake-workflows/rna-seq-star-deseq2
    # https://twitter.com/mikelove/status/918770188568363008
    few_coeffs = False if wildcards is None else len(get_contrast(wildcards)) < 10
    return 1 if len(samples) < 100 or few_coeffs else 6

def get_quant(w):
    quant =[]
    inputD = {}
    if ep_params.get('combine_units', True):
        sample_list = set(samples.index.get_level_values('sample'))
        quant = expand(join(quant_dir, "{sample}_x_{assembly}.salmon/quant.sf"), sample = sample_list, **w)
    else:
        # didn't combine units at the salmon level
        for s, u in samples.iterrows():
            sample, unit = u['sample'],u['unit']
            quant+=[join(input_dir, f"{sample}_{unit}_x_{w.assembly}.salmon/quant.sf")]
    inputD['quant'] = quant
    if deseq_params.get('gene_trans_map'):
        inputD['gene_trans_map'] = join(assembly_dir, f"{w.assembly}.fasta.gene_trans_map")
    return inputD


if deseq_params.get('gene_trans_map'):
    rule deseq2_init_tx2gene:
        input:
            unpack(get_quant),
        output:
            rds =join(deseq_dir, "{assembly}.deseq2.rds") #r data object
        params:
            samples=config["samples"]
        conda:
            "environment.yaml"
        log:
            join(LOGS_DIR,  'deseq2', "{assembly}.deseq2.init.log")
        benchmark:
            join(LOGS_DIR,  'deseq2', "{assembly}.deseq2.init.benchmark")
        threads: get_deseq2_threads()
        script:
            "deseq2-init-tx2gene.R"
else:
    rule deseq2_init_txOut:
        input:
            unpack(get_quant),
        output:
            rds =join(deseq_dir, "{assembly}.deseq2.rds") #r data object
        params:
            samples=config["samples"]
        conda:
            "environment.yaml"
        log:
            join(LOGS_DIR,  'deseq2', "{assembly}.deseq2.init.log")
        benchmark:
            join(LOGS_DIR,  'deseq2', "{assembly}.deseq2.init.benchmark")
        threads: get_deseq2_threads()
        script:
            "deseq2-init-txOut.R"

rule deseq2_pca:
    input:
        join(deseq_dir, "{assembly}.deseq2.rds")
    output:
        join(deseq_dir, "{assembly}.deseq2.pca.pdf")
        #report(join(deseq_dir, "{assembly}.deseq2.pca.pdf"), "../../report/pca.rst")
    params:
        pca_labels=deseq_params["pca"]["labels"]
    conda:
        "environment.yaml"
    log:
        join(LOGS_DIR, 'deseq2', "{assembly}.deseq2.pca.log")
    benchmark:
        join(LOGS_DIR, 'deseq2', "{assembly}.deseq2.pca.benchmark")
    script:
        "deseq2_plot_pca.R"


def get_contrast(wildcards):
    return deseq_params["contrasts"][wildcards.contrast]


rule deseq2:
    input:
        join(deseq_dir, "{assembly}.deseq2.rds")
    output:
        table=join(deseq_dir,"{assembly}.c{contrast}.deseq2.tsv"),
        sigTable=join(deseq_dir,"{assembly}.c{contrast}.deseq2.padj_0.1.tsv"),
        ma_plot=join(deseq_dir, "{assembly}.c{contrast}.deseq2.ma-plot.pdf"),
        #table=report(join(deseq_dir,"{contrast}.diffexp.tsv", "../../report/diffexp.rst")),
        #ma_plot=report(join(deseq_dir, "{contrast}.ma-plot.pdf", "../../report/ma.rst")),
    params:
        contrast=get_contrast
    conda:
        "environment.yaml"
    log:
        join(LOGS_DIR, 'deseq2', "{assembly}.c{contrast}.deseq2.log")
    benchmark:
        join(LOGS_DIR, 'deseq2', "{assembly}.c{contrast}.deseq2.benchmark")
    threads: get_deseq2_threads
    script:
        "deseq2.R"

