from os.path import join

logs_dir  = config['elvers_directories']['logs']
trimmomatic_elvers_params = config["trimmomatic"]['elvers_params']
trimmomatic_prog_params = config["trimmomatic"]['program_params']

# set output dir
#output_dir = elvers_params['outputs']['outdir']
trimmomatic_output_dir = os.path.dirname(trimmomatic_elvers_params['outputs']['output_files'][0])
# set default input dir, extensions
trimmomatic_input_dir = config['elvers_directories']['outdirs']['input_data']
#trimmomatic_ext = ['.fq.gz']
# get input we're using in this case
#trimmomatic_input_name = trimmomatic_prog_params.get('inputs', ['raw'])[0]
# update input_dir, ext
#trimmomatic_input_dir = trimmomatic_elvers_params['input_options'][trimmomatic_input_name].get('indir', trimmomatic_input_dir)
#trimmomatic_ext = trimmomatic_elvers_params['input_options'][trimmomatic_input_name]['extensions']['read'].get('common_ext', trimmomatic_ext)[0]

# find adapters --> use utils/find_input_file for this?
base_dir = config['elvers_directories']['base_dir']
pe_adapter = trimmomatic_prog_params['adapter_file']['pe_path']
se_adapter = trimmomatic_prog_params['adapter_file']['se_path']

if not os.path.isabs(pe_adapter):
    pe_adapter = join(base_dir, pe_adapter)
if not os.path.isabs(se_adapter):
    se_adapter = join(base_dir, se_adapter)

def get_pretrim(w):
    readsD = {}
    if not is_single_end(**w):
        readsD['r1'] = expand(join(trimmomatic_input_dir, '{sample}_{unit}_1.fq.gz'),**w)
        readsD['r2'] = expand(join(trimmomatic_input_dir, '{sample}_{unit}_2.fq.gz'),**w)
        return readsD
    return expand(join(trimmomatic_input_dir, '{sample}_{unit}_1.fq.gz'), **w)

rule trimmomatic_pe:
    """
    Trim reads from the sequencer by trimming or dropping low-quality reads.
    """
    input:
        unpack(get_pretrim)
    output:
        r1=join(trimmomatic_output_dir, "{sample}_{unit}_1.trim.fq.gz"),
        r2=join(trimmomatic_output_dir, "{sample}_{unit}_2.trim.fq.gz"),
        r1_unpaired=join(trimmomatic_output_dir, "{sample}_{unit}_1.se.trim.fq.gz"),
        r2_unpaired=join(trimmomatic_output_dir, "{sample}_{unit}_2.se.trim.fq.gz"),
    message:
        """--- Quality trimming PE read data with Trimmomatic. --- Output files will be in {trimmomatic_output_dir} """
    threads: trimmomatic_prog_params.get('cpu', 16)
    params:
        trimmer = (trimmomatic_prog_params['trim_cmd'].format(pe_adapter)).split(' '),
        extra = trimmomatic_prog_params.get('extra', '')
    log: join(logs_dir, 'trimmomatic/{sample}_{unit}_pe.log')
    benchmark: join(logs_dir, 'trimmomatic/{sample}_{unit}_pe.benchmark')
    conda: 'environment.yml'
    script:
        'trimmomatic-pe.py'

rule trimmomatic_se:
    """
    Trim reads from the sequencer by trimming or dropping low-quality reads.
    """
    input:
        get_pretrim
    output:
        r1=join(trimmomatic_output_dir, "{sample}_{unit}_se.trim.fq.gz"), 
    message:
        """--- Quality trimming SE read data with Trimmomatic."""
    threads: trimmomatic_prog_params.get('cpu', 16)
    params:
        trimmer = (trimmomatic_prog_params['trim_cmd'].format(se_adapter)).split(' '),
        extra = trimmomatic_prog_params.get('extra', '')
    log:join(logs_dir, 'trimmomatic/{sample}_{unit}_se.log')
    benchmark:join(logs_dir, 'trimmomatic/{sample}_{unit}_se.benchmark')
    conda: 'environment.yml'
    script:
        'trimmomatic-se.py'

